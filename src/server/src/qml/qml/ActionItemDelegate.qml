import QtQuick
import QtQuick.Layouts

Item {
    id: root
    height: 36

    property bool selected: false
    readonly property bool hovered: mouseArea.containsMouse

    required property string title
    required property string iconSource
    required property string shortcutLabel
    required property bool isSubmenu
    required property bool isDanger

    signal clicked()

    MouseArea {
        id: mouseArea
        anchors.fill: parent
        hoverEnabled: true
        onClicked: root.clicked()
    }

    Rectangle {
        anchors.fill: parent
        anchors.leftMargin: 6
        anchors.rightMargin: 6
        radius: 10
        color: {
            if (root.selected) return Theme.listItemSelectionBg
            if (root.hovered) return Theme.listItemHoverBg
            return "transparent"
        }
    }

    RowLayout {
        anchors.fill: parent
        anchors.leftMargin: 16
        anchors.rightMargin: 16
        spacing: 10

        // Action icon
        Item {
            visible: root.iconSource !== ""
            Layout.preferredWidth: 18
            Layout.preferredHeight: 18
            Layout.alignment: Qt.AlignVCenter

            ViciImage {
                anchors.fill: parent
                source: root.iconSource
            }
        }

        // Title
        Text {
            text: root.title
            color: {
                if (root.isDanger) return Theme.danger
                if (root.selected) return Theme.listItemSelectionFg
                return Theme.foreground
            }
            font.pointSize: Theme.regularFontSize
            elide: Text.ElideRight
            maximumLineCount: 1
            Layout.fillWidth: true
        }

        // Shortcut badge
        Rectangle {
            visible: root.shortcutLabel !== ""
            width: shortcutText.implicitWidth + 10
            height: shortcutText.implicitHeight + 4
            radius: 4
            color: Theme.divider
            Layout.alignment: Qt.AlignVCenter

            Text {
                id: shortcutText
                text: root.shortcutLabel
                color: Theme.textMuted
                font.pointSize: Theme.smallerFontSize - 1
                anchors.centerIn: parent
            }
        }

        // Submenu chevron
        Image {
            visible: root.isSubmenu
            source: Img.builtin("chevron-right")
            sourceSize.width: 12
            sourceSize.height: 12
            Layout.preferredWidth: 12
            Layout.preferredHeight: 12
            Layout.alignment: Qt.AlignVCenter
            opacity: 0.5
        }
    }
}
